// Saradush Combat Script

IF
	PickPocketFailed([ANYONE])
	GlobalLT("XA_LD_PP5000", "LOCALS", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LD_PP5000", "LOCALS", 1)
		DisplayStringHead(Myself, @0) /*~DIE!~*/
		Enemy()
		SetInterrupt(TRUE)
		Continue()
END

IF
	GlobalLT("XA_LD_EquipStaff", "LOCALS", 1)
	OR(3)
		See(NearestEnemyOf(Myself))
		AttackedBy([ANYONE], DEFAULT)
		PickPocketFailed([ANYONE])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LD_EquipStaff", "LOCALS", 1)
		EquipMostDamagingMelee()
		Enemy()
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("XA_LD_Morris", "LOCALS", 0)
	OR(3)
		See(NearestEnemyOf(Myself))
		AttackedBy([ANYONE], DEFAULT)
		PickPocketFailed([ANYONE])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayString(Myself,@1)  // Spell Trigger—Fired
		DisplayString(Myself,@3)  // ~Abi-Dalzim's Horrid Wilting~
		ApplySpell(NearestEnemyOf(Myself), WIZARD_ABI_DALZIMS_HORRID_WILTING)
		ApplySpell(NearestEnemyOf(Myself), WIZARD_ABI_DALZIMS_HORRID_WILTING)
		ApplySpell(NearestEnemyOf(Myself), WIZARD_ABI_DALZIMS_HORRID_WILTING)
		DisplayString(Myself,@4)  // Stoneskin
		ApplySpell(Myself, WIZARD_STONE_SKIN)			
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("XA_LD_Morris", "LOCALS", 0)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
		ApplySpell(Myself, WIZARD_TIME_STOP)	
		SetInterrupt(TRUE)
END

//{ Timestop Events

	//{ Step 1: Improved Alacrity
		IF
			Global("XA_LD_Morris", "LOCALS", 1)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				ApplySpell(Myself, WIZARD_IMPROVED_ALUCRITY)	
				SetInterrupt(TRUE)
		END
	//}
	
	//{ Step 2: Greater Malison
		IF
			Global("XA_LD_Morris", "LOCALS", 2)
			See(NearestEnemyOf(Myself))
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				ApplySpell(NearestEnemyOf(Myself), WIZARD_GREATER_MALISON)	
				SetInterrupt(TRUE)
				Continue()
		END
		
		IF
			Global("XA_LD_Morris", "LOCALS", 2)
		THEN
			RESPONSE #100
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
		END
	//}

	//{ Step 3: Protection From Energy
		IF
			Global("XA_LD_Morris", "LOCALS", 3)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				ApplySpell(Myself, WIZARD_PROTECTION_FROM_ENERGY)	
				SetInterrupt(TRUE)
		END
	//}

	//{ Step 4: Remove Magic, Chaos
		IF
			Global("XA_LD_Morris", "LOCALS", 4)
			See(NearestEnemyOf(Myself))
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				DisplayString(Myself,@2)  // Spell Trigger—Fired
				ApplySpell(NearestEnemyOf(Myself), WIZARD_REMOVE_MAGIC)
				ApplySpell(NearestEnemyOf(Myself), WIZARD_REMOVE_MAGIC)
				ApplySpell(NearestEnemyOf(Myself), WIZARD_REMOVE_MAGIC)	
				SetInterrupt(TRUE)
				Continue()
		END
		
		IF
			Global("XA_LD_Morris", "LOCALS", 4)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				ApplySpell(Myself, WIZARD_CHAOS)	
				SetInterrupt(TRUE)
		END
	//}
	
	//{ Step 5: Animate Dead, Domination
		IF
			Global("XA_LD_Morris", "LOCALS", 5)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				DisplayString(Myself,@2)  // Spell Trigger—Fired
				ApplySpell(Myself, WIZARD_ANIMATE_DEAD)
				ApplySpell(Myself, WIZARD_ANIMATE_DEAD)
				ApplySpell(Myself, WIZARD_ANIMATE_DEAD)	
				SetInterrupt(TRUE)
				Continue()
		END

		IF
			Global("XA_LD_Morris", "LOCALS", 5)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				DisplayString(Myself,@2)  // Spell Trigger—Fired
				ApplySpell(StrongestOf([PC]), WIZARD_DOMINATION)
				ApplySpell(StrongestOf([PC]), WIZARD_DOMINATION)
				ApplySpell(StrongestOf([PC]), WIZARD_DOMINATION)	
				SetInterrupt(TRUE)
		END
	//}

	//{ Step 6: Summon Planetar
		IF
			Global("XA_LD_Morris", "LOCALS", 6)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				ApplySpell(Myself, WIZARD_SUMMON_PLANATAR_GOOD)		
				SetInterrupt(TRUE)
		END
	//}

	//{ Step 7: Haste
		IF
			Global("XA_LD_Morris", "LOCALS", 7)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				ApplySpell(Myself, WIZARD_HASTE)		
				SetInterrupt(TRUE)
		END
	//}
	
	//{ Step 8: Timestop
		IF
			Global("XA_LD_Morris", "LOCALS", 8)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				Spell(Myself, WIZARD_TIME_STOP)	
				SetInterrupt(TRUE)
		END
	//}
	
	//{ Step 9: Second Salvo - Horrid Wilting
		IF
			Global("XA_LD_Morris", "LOCALS", 9)
			See(NearestEnemyOf(Myself))
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				DisplayString(Myself,@2)  // Spell Trigger—Fired
				ApplySpell(NearestEnemyOf(Myself), WIZARD_ABI_DALZIMS_HORRID_WILTING)
				ApplySpell(NearestEnemyOf(Myself), WIZARD_ABI_DALZIMS_HORRID_WILTING)
				ApplySpell(NearestEnemyOf(Myself), WIZARD_ABI_DALZIMS_HORRID_WILTING)	
				SetInterrupt(TRUE)
				Continue()
		END
		
		IF
			Global("XA_LD_Morris", "LOCALS", 9)
		THEN
			RESPONSE #100
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
		END
	//}

	//{ Step 10: Invisibility
		IF
			Global("XA_LD_Morris", "LOCALS", 10)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				ApplySpell(Myself, WIZARD_IMPROVED_INVISIBILITY)
				SetInterrupt(TRUE)
		END
	//}
	
	//{ Step 11: True Sight
		IF
			Global("XA_LD_Morris", "LOCALS", 11)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				ApplySpell(Myself, WIZARD_TRUE_SIGHT)
				SetInterrupt(TRUE)
		END
	//}

	//{ Step 12: Protection from Magic Weapons
		IF
			Global("XA_LD_Morris", "LOCALS", 12)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LD_Morris", "LOCALS", 1)
				Spell(Myself, WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
				SetInterrupt(TRUE)
		END
	//}

//}
	
	
//{ "Useful" Casts
	IF
		Global("Prep","LOCALS",0)
		!StateCheck(Myself,STATE_REALLY_DEAD)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			ApplySpell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
			ApplySpell(Myself,WIZARD_IMPROVED_MANTLE)  // SPWI808.SPL (Improved Mantle)
			ApplySpell(Myself,WIZARD_MIRROR_IMAGE)  // SPWI212.SPL (Mirror Image)
			SetGlobal("Prep","LOCALS",1)
	END

	IF
		Global("SpellTrigger","LOCALS",0)
		!StateCheck(Myself,STATE_REALLY_DEAD)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)  // SPWI319.SPL (Protection From Fire)
			DisplayString(Myself,43047)  // Spell Trigger—Fire Shield Red
			ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)  // SPWI418.SPL (Fire Shield (Red))
			DisplayString(Myself,40245)  // Spell Trigger—Globe of Invulnerability
			ReallyForceSpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)  // SPWI602.SPL (Globe of Invulnerability)
			SetGlobal("SpellTrigger","LOCALS",1)
	END

	IF
		See([GOODCUTOFF.0.0.0.0.SUMMONED])
		Global("summon","LOCALS",0)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			FaceObject(LastSeenBy(Myself))
			ReallyForceSpell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)  // SPWI605.SPL (Death Spell)
			SetGlobal("summon","LOCALS",1)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_TIME_STOP)  // SPWI909.SPL (Time Stop)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_TIME_STOP)  // SPWI909.SPL (Time Stop)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_METEOR_SWARM)  // SPWI911.SPL (Meteor Swarm)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_METEOR_SWARM)  // SPWI911.SPL (Meteor Swarm)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_GATE)  // SPWI905.SPL (Gate)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			ApplySpell(Myself,WIZARD_PROTECTION_FROM_EVIL)  // SPWI113.SPL (Protection From Evil)
			Spell(Myself,WIZARD_GATE)  // SPWI905.SPL (Gate)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)  // SPWI812.SPL (Abi-Dalzim's Horrid Wilting)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)  // SPWI812.SPL (Abi-Dalzim's Horrid Wilting)
	END

	IF
		See(NearestEnemyOf(Myself))
		StateCheck(Myself,STATE_SILENCED)
		HaveSpell(WIZARD_VOCALIZE)  // SPWI219.SPL (Vocalize)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_VOCALIZE)  // SPWI219.SPL (Vocalize)
	END

	IF
		Allegiance(Myself,ENEMY)
		Detect([PC])
		!See(LastSeenBy(Myself))
		HaveSpell(WIZARD_TRUE_SIGHT)  // SPWI609.SPL (True Sight)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_TRUE_SIGHT)  // SPWI609.SPL (True Sight)
	END

	IF
		Allegiance(Myself,ENEMY)
		Detect([PC])
		!See(LastSeenBy(Myself))
		!HaveSpell(WIZARD_TRUE_SIGHT)  // SPWI609.SPL (True Sight)
		Delay(10)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)  // SPWI609.SPL (True Sight)
	END

	IF
		See(NearestEnemyOf(Myself))
		Global("Prep","LOCALS",1)
		!StateCheck(Myself,STATE_REALLY_DEAD)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_SPELL_TURNING)  // SPWI701.SPL (Spell Turning)
			SetGlobal("Prep","LOCALS",2)
	END

	IF
		!HasBounceEffects(Myself)
		!HasImmunityEffects(Myself)
		HaveSpell(WIZARD_SPELL_TURNING)  // SPWI701.SPL (Spell Turning)
		Allegiance(Myself,ENEMY)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_SPELL_TURNING)  // SPWI701.SPL (Spell Turning)
	END

	IF
		See(NearestEnemyOf(Myself))
		TookDamage()
		Global("Prep2","LOCALS",0)
		!StateCheck(Myself,STATE_REALLY_DEAD)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			DisplayString(Myself,43050)  // Chain Contingency—Improved Mantle
			ApplySpell(Myself,WIZARD_IMPROVED_MANTLE)  // SPWI808.SPL (Improved Mantle)
			DisplayString(Myself,3302)  // Chain Contingency—Spell Turning
			ApplySpell(Myself,WIZARD_SPELL_TURNING)  // SPWI701.SPL (Spell Turning)
			DisplayString(Myself,40240)  // Chain Contingency—Mislead
			ApplySpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)  // SPWI602.SPL (Globe of Invulnerability)
			SetGlobal("Prep2","LOCALS",1)
	END

	IF
		See(NearestEnemyOf(Myself))
		TookDamage()
		Global("Prep2","LOCALS",1)
		!StateCheck(Myself,STATE_REALLY_DEAD)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			DisplayString(Myself,40252)  // Contingency—Protection From Magical Weapons
			ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // SPWI611.SPL (Protection From Magical Weapons)
			SetGlobal("Prep2","LOCALS",2)
	END

	IF
		Allegiance(Myself,ENEMY)
		!HasBounceEffects(Myself)
		!HasImmunityEffects(Myself)
		HaveSpell(WIZARD_SPELL_TURNING)  // SPWI701.SPL (Spell Turning)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_SPELL_TURNING)  // SPWI701.SPL (Spell Turning)
	END

	IF
		See(SecondNearestEnemyOf(Myself))
		Global("SpellTrigger","LOCALS",0)
		!StateCheck(Myself,STATE_REALLY_DEAD)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			DisplayString(Myself,39969)  // Spell Sequencer—Fired
			DisplayString(Myself,44899)  // Spell Sequencer—Confusion
			ReallyForceSpell(SecondNearestEnemyOf(Myself),WIZARD_CONFUSION)  // SPWI401.SPL (Confusion)
			DisplayString(Myself,362)  // Spell Sequencer—Fireball
			ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FIREBALL)  // SPWI304.SPL (Fireball)
			DisplayString(Myself,44871)  // Spell Sequencer—Protection From Evil
			ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_EVIL)  // SPWI113.SPL (Protection From Evil)
			SetGlobal("SpellTrigger","LOCALS",1)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_MAZE)  // SPWI813.SPL (Maze)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_MAZE)  // SPWI813.SPL (Maze)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_NPC_SYMBOL_STUN)  // SPWI898.SPL (Symbol, Stun)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_NPC_SYMBOL_STUN)  // SPWI898.SPL (Symbol, Stun)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_NPC_SYMBOL_FEAR)  // SPWI899.SPL (Symbol, Fear)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_NPC_SYMBOL_FEAR)  // SPWI899.SPL (Symbol, Fear)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_NPC_SYMBOL_DEATH)  // SPWI897.SPL (Symbol, Death)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_NPC_SYMBOL_DEATH)  // SPWI897.SPL (Symbol, Death)
	END

	IF
		HaveSpell(WIZARD_SUN_FIRE)  // SPWI523.SPL (Sunfire)
		Range(SecondNearestEnemyOf(Myself),10)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_SUN_FIRE)  // SPWI523.SPL (Sunfire)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_WAIL_OF_THE_BANSHEE)  // SPWI913.SPL (Wail of the Banshee)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_WAIL_OF_THE_BANSHEE)  // SPWI913.SPL (Wail of the Banshee)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_SHADOW_DOOR)  // SPWI505.SPL (Shadow Door)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_SHADOW_DOOR)  // SPWI505.SPL (Shadow Door)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_FINGER_OF_DEATH)  // SPWI713.SPL (Finger of Death)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_FINGER_OF_DEATH)  // SPWI713.SPL (Finger of Death)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_SUMMON_EFREET)  // SPWI717.SPL (Summon Efreeti)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_SUMMON_EFREET)  // SPWI717.SPL (Summon Efreeti)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_FLESH_TO_STONE)  // SPWI604.SPL (Flesh to Stone)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_FLESH_TO_STONE)  // SPWI604.SPL (Flesh to Stone)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_DISINTEGRATE)  // SPWI616.SPL (Disintegrate)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_DISINTEGRATE)  // SPWI616.SPL (Disintegrate)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_DOMINATION)  // SPWI506.SPL (Domination)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_DOMINATION)  // SPWI506.SPL (Domination)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_FIRE_SHIELD_RED)  // SPWI418.SPL (Fire Shield (Red))
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(Myself,WIZARD_FIRE_SHIELD_RED)  // SPWI418.SPL (Fire Shield (Red))
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_SLOW)  // SPWI312.SPL (Slow)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_SLOW)  // SPWI312.SPL (Slow)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_FLAME_ARROW)  // SPWI303.SPL (Flame Arrow)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_FLAME_ARROW)  // SPWI303.SPL (Flame Arrow)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_MELF_ACID_ARROW)  // SPWI211.SPL (Melf's Acid Arrow)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_MELF_ACID_ARROW)  // SPWI211.SPL (Melf's Acid Arrow)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_LARLOCH_MINOR_DRAIN)  // SPWI119.SPL (Larloch's Minor Drain)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_LARLOCH_MINOR_DRAIN)  // SPWI119.SPL (Larloch's Minor Drain)
	END

	IF
		See(NearestEnemyOf(Myself))
		HaveSpell(WIZARD_POLYMORPH_OTHER)  // SPWI415.SPL (Polymorph Other)
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			Spell(NearestEnemyOf(Myself),WIZARD_POLYMORPH_OTHER)  // SPWI415.SPL (Polymorph Other)
	END

	IF
		See(NearestEnemyOf(Myself))
		GlobalGT("XA_LD_Morris", "LOCALS", 12)
	THEN
		RESPONSE #100
			AttackReevaluate(NearestEnemyOf(Myself),15)
	END
//}
