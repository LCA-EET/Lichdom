/* Copyright Notice
Copyright © 2024 Daniel Valle, Lichdom
 
Licensed under [Creative Commons Attribution-ShareAlike 4.0 International License]

See https://creativecommons.org/licenses/by-sa/4.0/ for details.
*/

/* ARTWORK CREDITS

Lich, by ArtDeepMind
https://www.deviantart.com/artdeepmind/art/Lich-545596034

*/

BACKUP ~Lichdom/backup~

SUPPORT ~Daniel Valle, dan@danielvalle.net~

AUTO_TRA ~%MOD_FOLDER%/tra/%s~ //automatically associate .tras to their respective .D files
AUTO_EVAL_STRINGS
LANGUAGE ~English~ ~english~ ~%MOD_FOLDER%/tra/english/xalich.tra~
LANGUAGE ~Simplified Chinese（by mogui45）~ ~schinese~ ~%MOD_FOLDER%/tra/schinese/xalich.tra~
BEGIN
@0 /* ~Lichdom, for Baldur's Gate EET.~ */

OUTER_SET sod_carryover = 0
OUTER_SET bg2_carryover = 0
OUTER_SET xalddebug = 0
OUTER_SET xa_tempID = 0

INCLUDE ~%MOD_FOLDER%/functions.tph~

OUTER_SET xald_use_defaults = 0

LAF xa_determine_quantity
	INT_VAR
		xa_min_qty = 0
		xa_max_qty = 1
		xa_str_ref = RESOLVE_STR_REF(@72)
	RET
		xald_use_defaults = xa_quantity
END

ACTION_IF (xald_use_defaults = 1) THEN BEGIN
	INCLUDE ~%MOD_FOLDER%/defaults.tph~
END

ACTION_IF (GAME_IS ~eet~) THEN BEGIN
	OUTER_SET isEET = 1
	PRINT @1  // ~Enhanced Edition Trilogy (EET) mod has been detected.~ 
	INCLUDE ~%MOD_FOLDER%/sod/xasod.tph~
	INCLUDE ~%MOD_FOLDER%/bg2/xabg2.tph~
	INCLUDE ~%MOD_FOLDER%/tob/xatob.tph~
END ELSE BEGIN
	OUTER_SET isEET = 0
	PRINT @51  // ~EET was not detected. Patching individual game(s) accordingly...~ 
	ACTION_IF(GAME_INCLUDES ~sod~) THEN BEGIN
		PRINT @49  // ~Siege of Dragonspear detected.~ 
		INCLUDE ~%MOD_FOLDER%/sod/xasod.tph~
	END
	ACTION_IF(GAME_IS ~bg2ee~) THEN BEGIN
		PRINT @50  // ~Baldur's Gate II Enhanced Edition detected.~ 
		INCLUDE ~%MOD_FOLDER%/bg2/xabg2.tph~
		INCLUDE ~%MOD_FOLDER%/tob/xatob.tph~
	END
END

ACTION_IF (xald_use_defaults = 0) THEN BEGIN
	OUTER_SET xald_assistant = 0
	LAF xa_determine_quantity
		INT_VAR
			xa_min_qty = 0
			xa_max_qty = 1
			xa_str_ref = RESOLVE_STR_REF(@67)
		RET
			xald_assistant = xa_quantity
	END
END
ACTION_IF (xald_assistant = 1) THEN BEGIN
	INCLUDE ~%MOD_FOLDER%/assistant/xaassistant.tph~
END

INCLUDE ~%MOD_FOLDER%/compat/compat.tph~

PRINT @57  // ~Installation was successful. Enjoy!~

