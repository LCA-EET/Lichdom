PRINT @55  // ~Processing Shadows of Amn carry forwards...~
SILENT

OUTER_SET bg2_carryover = 1

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/bg2/baf~ 

ACTION_IF(xalddebug = 1) THEN BEGIN
	EXTEND_TOP ~baldur.bcs~ ~override/xalddbg.bcs~
END

//{ Config file processing

	//{ Number of Crowns for Sale
		ACTION_IF (xald_use_defaults = 0) THEN BEGIN
			OUTER_SET xald_crowns = 0
			LAF xa_determine_quantity
				INT_VAR
					xa_min_qty = 0
					xa_max_qty = 10
					xa_str_ref = RESOLVE_STR_REF(@36)
				RET
					xald_crowns = xa_quantity
			END
		END
		SILENT
		COPY_EXISTING ~garlena.sto~ ~override~
		LPF ADD_STORE_ITEM_EX
			INT_VAR
				stack = xald_crowns
			STR_VAR
				item_name = ~xaldcrwn~
				flags = ~IDENTIFIED~
		END
		SILENT
	//}
	
	//{ Chance to Break
		ACTION_IF (xald_use_defaults = 0) THEN BEGIN
			OUTER_SET xald_breakchance = 0
			LAF xa_determine_quantity
				INT_VAR
					xa_min_qty = 0
					xa_max_qty = 100
					xa_str_ref = RESOLVE_STR_REF(@74)
				RET
					xald_breakchance = xa_quantity
			END
		END
		SILENT
	//}
	
	//{ Ritual Cost
		ACTION_IF (xald_use_defaults = 0) THEN BEGIN
			OUTER_SET xald_cost = 0
			LAF xa_determine_quantity
				INT_VAR
					xa_min_qty = 40000
					xa_max_qty = 200000
					xa_str_ref = RESOLVE_STR_REF(@37)
				RET
					xald_cost = xa_quantity
			END
		END
		COPY_EXISTING ~xaldgd.dlg~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY ~6686~ ~%xald_cost%~
		END
	//}
	
	//{ Max Number of Rituals
		ACTION_IF (xald_use_defaults = 0) THEN BEGIN
			OUTER_SET xald_maxrituals = 0
			LAF xa_determine_quantity
				INT_VAR
					xa_min_qty = 1
					xa_max_qty = 10
					xa_str_ref = RESOLVE_STR_REF(@21)
				RET
					xald_maxrituals = xa_quantity
			END
		END
		COPY_EXISTING ~xaldmaxt.bcs~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY ~6686~ ~%xald_maxrituals%~
		END
		
	//}
//}
SILENT
//{ Allows the protagonist to die (in conjunction with the modifications to BALDUR.BCS)~
	COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~ 
	WRITE_LONG 0x14 THIS|16
	BUT_ONLY
//}

//{ Add lich dust to liches in BG2 / ToB
	ACTION_DEFINE_ARRAY lich_cre
	BEGIN
		amlich01
		cedelich
		celich
		demilich
		dpmon02
		firlch01
		gorlic01
		gorpri1
		grvlch01
		hldemi
		hlkang
		hllich
		hlshade
		hlshang
		jarlich
		lich01
		lichel01
		melsum06
		mindal01
		nevaziah
		ohbdli01
		ohbdli02
		ohhalhoo
		ohhgoxxa
		senlich
		udtrap04
	END
		
	ACTION_PHP_EACH lich_cre AS _ => lich_ref
	BEGIN
		COPY_EXISTING ~%lich_ref%.cre~ ~override~
		ADD_CRE_ITEM ~xalddust~ #0 #0 #0 ~identified&unstealable~ ~inv15~
	END
//}

//{ 2DA 
	// thanks, Kelian
	OUTER_SET upper = RESOLVE_STR_REF(@22)
	COPY_EXISTING ~racetext.2da~ ~override~
	COUNT_2DA_COLS cols
	COUNT_2DA_ROWS cols rows
	INSERT_2DA_ROW rows 6 ~LICH 150 8457 -1 %upper% -1~
//}

//{ BAM
	COPY ~%MOD_FOLDER%/bg2/bam~ ~override~
//}

//{ BMP
	COPY ~%MOD_FOLDER%/bg2/bmp~ ~override~
//}

//{ CRE
	COPY ~%MOD_FOLDER%/bg2/cre/xaldgd.cre~ ~override~
	SAY NAME1 @19
	SAY NAME2 @19
	WRITE_ASCII 0x1EA8 ~xaldbag0~ #8
	WRITE_ASCII 0x250 ~xaldgdfa~ #8
//}

//{ ITM
	//{ Morris' Bag of Holding (SoA)
		COPY_EXISTING ~bag31.itm~ ~override/xaldbag0.itm~
		COPY_EXISTING ~bag31.sto~ ~override/xaldbag0.sto~
	//}
	
	COPY ~%MOD_FOLDER%/bg2/itm/xaldcrwn.itm~ ~override~
	SAY NAME1 @33
	SAY NAME2 @33
	SAY DESC @34

	COPY ~%MOD_FOLDER%/bg2/itm/xaldclaw.itm~ ~override~
	SAY NAME1 @16
	SAY NAME2 @16

	COPY ~%MOD_FOLDER%/bg2/itm/xaldcla2.itm~ ~override~
	SAY NAME1 @16
	SAY NAME2 @16

	//{ Create Phylacteries
		OUTER_SET xa_phylactery_index = 1
		OUTER_WHILE (xa_phylactery_index <= xald_maxrituals) BEGIN
			COPY ~%MOD_FOLDER%/bg2/itm/xaldphyl.itm~ ~override/xaldph%xa_phylactery_index%.itm~
			WRITE_ASCII 0x3A ~xaiphyl~ #8 // icon
			WRITE_ASCII 0x58 ~xaphyl~ #8 // desc image
			WRITE_LONG 0x8 RESOLVE_STR_REF(@2) // General Name
			WRITE_LONG 0xC RESOLVE_STR_REF(@2) // Identified Name
			WRITE_LONG 0x50 RESOLVE_STR_REF(@3) // General Description
			WRITE_LONG 0x54 RESOLVE_STR_REF(@3) // Identified Description
	
			COPY ~%MOD_FOLDER%/bg2/itm/xaldphyd.itm~ ~override/xaldpd%xa_phylactery_index%.itm~
			WRITE_ASCII 0x3A ~xaiphyl~ #8
			WRITE_ASCII 0x58 ~xaphyl~ #8 // desc image
			WRITE_LONG 0x8 RESOLVE_STR_REF(@23) // General Name
			WRITE_LONG 0xC RESOLVE_STR_REF(@23) // Identified Name
			WRITE_LONG 0x50 RESOLVE_STR_REF(@24) // General Description
			WRITE_LONG 0x54 RESOLVE_STR_REF(@24) // Identified Description
			
			OUTER_SET xa_phylactery_index = (xa_phylactery_index + 1)
		END	
	//}
	
	COPY_EXISTING ~plot16b.itm~ ~override/xaldlc00.itm~
	SAY NAME1 @25
	SAY NAME2 @25
	SAY DESC ~The cost to perform the ritual is %xald_cost% gold.~
//}

//{ Crown Turn Undead Immunity
	ACTION_IF (xald_use_defaults = 0) THEN BEGIN
		OUTER_SET xald_immune = 0
		LAF xa_determine_quantity
			INT_VAR
				xa_min_qty = 0
				xa_max_qty = 1
				xa_str_ref = RESOLVE_STR_REF(@69)
			RET
				xald_immune = xa_quantity
		END
	END
	ACTION_IF(xald_immune = 1) THEN BEGIN
		COPY_EXISTING ~xaldcrwn.itm~ ~override~
		WRITE_BYTE 0x5F4 100
		WRITE_BYTE 0x624 100
	END
//}

//{ SPL
	//{ Lich Resurrection
		COPY ~%MOD_FOLDER%/bg2/spl/xaldrez.spl~ ~override~
		SAY NAME1 @71
	//}
	
	//{ Examine Phylactery
		COPY ~%MOD_FOLDER%/bg2/spl/xaldpchk.spl~ ~override~
		SAY NAME1 @75
	//}

	//{ Lichdom Assistant
		COPY ~%MOD_FOLDER%/bg2/spl/xaldasst.spl~ ~override~
		SAY NAME1 @7
		SAY UNIDENTIFIED_DESC @8
		SAY DESC @8
	//}

	//{ Lich Mental Senses
		COPY ~%MOD_FOLDER%/bg2/spl/xaldls00.spl~ ~override~
		SAY NAME1 @18
		SAY UNIDENTIFIED_DESC @18
		SAY DESC @18
		
		COPY ~%MOD_FOLDER%/bg2/spl/xaldls01.spl~ ~override~
		SAY NAME1 @18
		SAY UNIDENTIFIED_DESC @18
		SAY DESC @18
	//}
	
	//{ Paralyzing Touch
		COPY ~%MOD_FOLDER%/bg2/spl/xaldpt.spl~ ~override~
		SAY NAME1 @16
		SAY UNIDENTIFIED_DESC @68
		SAY DESC @68
	//}
	
	
	//{ Lich Spell Immunities
		COPY ~%MOD_FOLDER%/bg2/spl/xaldsi.spl~ ~override/xaldsi01.spl~
		WRITE_LONG 0x9E 1
		SAY NAME1 @12
		SAY UNIDENTIFIED_DESC @12
		SAY DESC @12
		
		COPY_EXISTING ~xaldsi01.spl~ ~override/xaldsi02.spl~
		WRITE_LONG 0x9E 2
		
		COPY_EXISTING ~xaldsi01.spl~ ~override/xaldsi03.spl~
		WRITE_LONG 0x9E 3
		
		COPY_EXISTING ~xaldsi01.spl~ ~override/xaldsi04.spl~
		WRITE_LONG 0x9E 4
		
		COPY_EXISTING ~xaldsi01.spl~ ~override/xaldsi05.spl~
		WRITE_LONG 0x9E 5
	//}

	//{ Lich Magic Resistance Boost
		COPY ~%MOD_FOLDER%/bg2/spl/xaldmr.spl~ ~override/xaldmr01.spl~
		WRITE_LONG 0x9E 5
		SAY NAME1 @11
		SAY UNIDENTIFIED_DESC @11
		SAY DESC @11
	
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr02.spl~
		WRITE_LONG 0x9E 10
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr03.spl~
		WRITE_LONG 0x9E 20
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr04.spl~
		WRITE_LONG 0x9E 25
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr05.spl~
		WRITE_LONG 0x9E 30
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr06.spl~
		WRITE_LONG 0x9E 33
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr07.spl~
		WRITE_LONG 0x9E 40
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr08.spl~
		WRITE_LONG 0x9E 50
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr09.spl~
		WRITE_LONG 0x9E 60
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr10.spl~
		WRITE_LONG 0x9E 70
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr11.spl~
		WRITE_LONG 0x9E 75
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr12.spl~
		WRITE_LONG 0x9E 80
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr13.spl~
		WRITE_LONG 0x9E 90
		
		COPY_EXISTING ~xaldmr01.spl~ ~override/xaldmr14.spl~
		WRITE_LONG 0x9E 100
	//}
	
	//{ Lich Mental Boost
		COPY ~%MOD_FOLDER%/bg2/spl/xaldiwc.spl~ ~override/xaldiwc0.spl~
		WRITE_LONG 0x9E 1
		WRITE_LONG 0xCE 1
		WRITE_LONG 0xFE 1
		SAY NAME1 @10
		SAY UNIDENTIFIED_DESC @10
		SAY DESC @10
		
		COPY_EXISTING ~xaldiwc0.spl~ ~override/xaldiwc1.spl~ 
		WRITE_LONG 0x9E 2
		WRITE_LONG 0xCE 2
		WRITE_LONG 0xFE 2
	//}
	
	//{ Lich AC Modification
		COPY ~%MOD_FOLDER%/bg2/spl/xaldac.spl~ ~override/xaldac00.spl~
		SAY NAME1 @9
		SAY UNIDENTIFIED_DESC @9
		SAY DESC @9
		WRITE_BYTE 0x9E 253 // -3, Crushing
		WRITE_LONG 0xCE 3 // Piercing
		WRITE_LONG 0xFE 3 // Slashing
		
		COPY_EXISTING ~xaldac00.spl~ ~override/xaldac01.spl~
		WRITE_BYTE 0x9E 255 // -1, Crushing
		WRITE_LONG 0xCE 1 // Piercing
		WRITE_LONG 0xFE 1 // Slashing
		
		COPY_EXISTING ~xaldac00.spl~ ~override/xaldac02.spl~
		WRITE_BYTE 0x9E 254 // -2, Crushing
		WRITE_LONG 0xCE 2 // Piercing
		WRITE_LONG 0xFE 2 // Slashing
		
		COPY_EXISTING ~xaldac00.spl~ ~override/xaldac03.spl~
		WRITE_BYTE 0x9E 252 // -4, Crushing
		WRITE_LONG 0xCE 4 // Piercing
		WRITE_LONG 0xFE 4 // Slashing
		
		COPY_EXISTING ~xaldac00.spl~ ~override/xaldac04.spl~
		WRITE_BYTE 0x9E 251 // -5, Crushing
		WRITE_LONG 0xCE 5 // Piercing
		WRITE_LONG 0xFE 5 // Slashing
	//}
	
	//{ Lich Amination
		COPY ~%MOD_FOLDER%/bg2/spl/xaldanim.spl~ ~override~
		SAY NAME1 @13
		SAY UNIDENTIFIED_DESC @13
		SAY DESC @13
	//}
	
	//{ Portrait Change (Lich)
		COPY ~%MOD_FOLDER%/bg2/spl/xaplich.spl~ ~override~
	//}

	//{ Lich Death
		COPY ~%MOD_FOLDER%/bg2/spl/xalddest.spl~ ~override~
		SAY NAME1 @14
		SAY DESC @14
	//}
	
	//{ Lich Transformation 
		COPY ~%MOD_FOLDER%/bg2/spl/xaldlich.spl~ ~override~
		SAY NAME1 @6
		SAY DESC @6
	//}
	
	//{ Rename Paladin to Fallen Paladin
		COPY ~%MOD_FOLDER%/bg2/spl/xaldfpal.spl~ ~override~
	//}
	
	//{ Rename Paladin to Fallen Ranger
		COPY ~%MOD_FOLDER%/bg2/spl/xaldfran.spl~ ~override~
	//}
//}

//{ Transformation Scripts
	COPY_EXISTING ~xaldtr01.bcs~ ~override/xaldtr02.bcs~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY Player1 Player2
	END
	
	COPY_EXISTING ~xaldtr01.bcs~ ~override/xaldtr03.bcs~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY Player1 Player3
	END

	COPY_EXISTING ~xaldtr01.bcs~ ~override/xaldtr04.bcs~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY Player1 Player4
	END
	
	COPY_EXISTING ~xaldtr01.bcs~ ~override/xaldtr05.bcs~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY Player1 Player5
	END
	
	COPY_EXISTING ~xaldtr01.bcs~ ~override/xaldtr06.bcs~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY Player1 Player6
	END
	
//}

